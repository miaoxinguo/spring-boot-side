buildscript {
    repositories {
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.1.RELEASE")
    }
}

allprojects {
    group 'com.github.miaoxinguo'

    //定义版本号
    ext {
//        logbackVersion = '1.1.9'
//        junitVersion = '4.12'
        druidVersion = '1.0.26'
        hibernateValidatorVersion='5.2.4.Final'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    [compileJava, javadoc, compileTestJava]*.options*.encoding = 'UTF-8'

    repositories {
        mavenLocal()
        jcenter()
    }

    // 通用依赖
    dependencies {
        compile 'com.github.miaoxinguo:common:1.0-SNAPSHOT'
        compile 'com.github.miaoxinguo:mybatis-plugin:1.0-SNAPSHOT'

//        testCompile group: 'junit', name: 'junit', version: "${junitVersion}"
    }

    // 设置是否下载源码和文档
    idea {
        module {
            downloadJavadoc = false
            downloadSources = false
        }
    }
}

project(':sbs-mybatis-generator') {
    dependencies {
        compile group: 'org.mybatis.generator', name: 'mybatis-generator-core', version: '1.3.5'
    }
}

// filter by project name
configure(subprojects.findAll {it.name == 'sbs-web' || it.name == 'sbs-admin'}) {
    apply plugin: 'org.springframework.boot'

    dependencies {
        compile("org.springframework.boot:spring-boot-starter-aop")
        compile("org.springframework.boot:spring-boot-starter-jdbc")
        compile("org.springframework.boot:spring-boot-starter-web")
        compile("org.springframework.boot:spring-boot-starter-data-redis")
        compile 'org.mybatis.spring.boot:mybatis-spring-boot-starter:1.2.0'
        compile group: 'com.alibaba', name: 'druid', version: "${druidVersion}"

        testCompile("org.springframework.boot:spring-boot-starter-test")

        compileOnly "org.springframework.boot:spring-boot-configuration-processor"
    }

    compileJava.dependsOn(processResources)
}
