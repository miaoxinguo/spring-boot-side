buildscript {
    repositories {
        jcenter()
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.0.RELEASE")
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
    }
}

allprojects {
    group 'io.github.miaoxinguo.sbs'
    version '1.0-SNAPSHOT'

    //定义版本号
    ext {
        springVersion = '4.3.2.RELEASE'
        slf4jVersion = '1.7.21'
        logbackVersion = '1.1.7'
        junitVersion = '4.12'
        druidVersion = '1.0.26'
        hibernateValidatorVersion='5.2.4.Final'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    [compileJava, javadoc, compileTestJava]*.options*.encoding = 'UTF-8'

    repositories {
        jcenter()
    }

    // 通用依赖
    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: "${slf4jVersion}"

        runtime group: 'ch.qos.logback', name: 'logback-core', version: "${logbackVersion}"
        runtime group: 'ch.qos.logback', name: 'logback-classic', version: "${logbackVersion}"

        testCompile group: 'junit', name: 'junit', version: "${junitVersion}"
    }

}

project(':sbs-core') {
    dependencies {
        compile group: 'com.alibaba', name: 'fastjson', version: '1.2.16'

        compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
        compile group: 'org.apache.httpcomponents', name: 'httpasyncclient', version: '4.1.2'
        compile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.2'

        compile group: 'org.hibernate', name: 'hibernate-validator', version: "${hibernateValidatorVersion}"

        compile group: 'org.springframework', name: 'spring-context', version: "${springVersion}"
    }
}

project(':sbs-mybatis') {
    dependencies {
        compile project(":sbs-core")

        compile group: 'org.springframework', name: 'spring-jdbc', version: "${springVersion}"
        compile group: 'org.mybatis', name: 'mybatis', version: '3.4.1'
        compile group: 'org.mybatis', name: 'mybatis-spring', version: '1.3.0'

        runtime 'mysql:mysql-connector-java:5.1.35'
    }
}

project(':sbs-mybatis-generator') {
    dependencies {
        compile project(":sbs-mybatis")

        compile group: 'org.mybatis.generator', name: 'mybatis-generator-core', version: '1.3.5'
    }
}

project(':sbs-biz') {
    dependencies {
        compile project(":sbs-core")
        compile project(":sbs-mybatis")

        compile 'org.quartz-scheduler:quartz:2.2.1'
    }
}

// filter by project name
configure(subprojects.findAll {it.name == 'sbs-server' || it.name == 'sbs-admin'}) {
    apply plugin: 'spring-boot'

    dependencies {
        compile project(":sbs-biz")
        apply plugin: 'propdeps'

        compile("org.springframework.boot:spring-boot-starter-aop")
        compile("org.springframework.boot:spring-boot-starter-web")
        compile("org.springframework.boot:spring-boot-starter-data-redis")
        compile group: 'com.alibaba', name: 'druid', version: "${druidVersion}"

        testCompile("org.springframework.boot:spring-boot-starter-test")

        optional "org.springframework.boot:spring-boot-configuration-processor"
    }

    compileJava.dependsOn(processResources)
}

project(':sbs-server') {

}

project(':sbs-admin') {

}