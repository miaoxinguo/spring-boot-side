buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.0.RELEASE")
    }
}

allprojects {
    group 'io.github.miaoxinguo.sbs'
    version '1.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    [compileJava, javadoc, compileTestJava]*.options*.encoding = 'UTF-8'

    //定义版本号
    ext {
        springVersion = '3.2.11.RELEASE'
        hibernateVersion='4.3.1.Final'
    }

    repositories {
        jcenter()
    }

    // 通用依赖
    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'

        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

}

project(':sbs-core') {
    // jar包的名字
    archivesBaseName = 'sbs-core'

    dependencies {
        compile group: 'com.alibaba', name: 'fastjson', version: '1.2.16'
        compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
        compile group: 'org.apache.httpcomponents', name: 'httpasyncclient', version: '4.1.2'
        compile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.2'
        compile 'org.hibernate:hibernate-validator:5.1.3.Final'
    }
}

project(':sbs-mybatis') {
    archivesBaseName = 'sbs-mybatis'

    dependencies {
        compile project(":sbs-core")

        compile("org.mybatis.spring.boot:mybatis-spring-boot-starter:1.1.1")

        runtime 'mysql:mysql-connector-java:5.1.35'
    }
}

project(':sbs-mybatis-generator') {
    dependencies {
        compile project(":sbs-mybatis")

        compile group: 'org.mybatis.generator', name: 'mybatis-generator-core', version: '1.3.5'
    }
}

project(':sbs-biz') {
    archivesBaseName = 'sbs-biz'

    dependencies {
        compile project(":sbs-core")
        compile project(":sbs-mybatis")

        compile 'org.quartz-scheduler:quartz:2.2.1'
    }
}

project(':sbs-server') {
    archivesBaseName = 'sbs-server'

    apply plugin: 'spring-boot'

    dependencies {
        compile project(":sbs-biz")

        compile("org.springframework.boot:spring-boot-starter-web")
        compile("org.springframework.boot:spring-boot-starter-aop")
        compile("org.springframework.boot:spring-boot-starter-data-redis")
        compile group: 'com.alibaba', name: 'druid', version: '1.0.26'

        runtime 'mysql:mysql-connector-java:5.1.35'

        testCompile("org.springframework.boot:spring-boot-starter-test")
    }
}

project(':sbs-admin') {
    archivesBaseName = 'sbs-admin'

    apply plugin: 'spring-boot'

    dependencies {
        compile project(":sbs-biz")

        compile("org.springframework.boot:spring-boot-starter-web")
        compile("org.springframework.boot:spring-boot-starter-aop")
        compile group: 'com.alibaba', name: 'druid', version: '1.0.26'

        compile 'org.hibernate:hibernate-validator:5.1.3.Final'

        runtime 'mysql:mysql-connector-java:5.1.35'

        testCompile("org.springframework.boot:spring-boot-starter-test")
    }
}